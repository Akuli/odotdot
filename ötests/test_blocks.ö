var test = (import "utils").test;
var throws = (import "utils").throws;


test "definition_scope" {
    # not much more can be done, everything relies on this stuff working correctly
    assert ({}.definition_scope `is_instance_of` Scope);
    assert ({}.definition_scope `same_object` {print "Hi";}.definition_scope);
    var a = 123;
    assert (({}.definition_scope.local_vars.get "a") == 123);
};

test "run and run_with_return" {
    var scope = (new Scope {}.definition_scope);
    var ran = 0;

    {
        throws VariableError { return "lol"; };
        ran = (ran + 1);
    }.run scope;
    assert (ran == 1);

    assert (({
        ran = (ran + 1);
        return "lol";
        ran = (ran + 666);   # must not run
    }.run_with_return scope) == "lol");
    assert (ran == 2);
    # TODO: the return variable should get deleted from the scope
    # right now it doesn't get deleted

    throws ValueError {
        {
            ran = (ran + 1);
            throw (new ValueError "oh shit");
            ran = (ran + 666);
        }.run scope;
    };
    assert (ran == 3);

    throws ValueError {
        var _ = ({
            ran = (ran + 1);
            throw (new ValueError "oh shit");
            ran = (ran + 666);
        }.run_with_return scope);
    };
    assert (ran == 4);

    # this throws an error if run_with_return returns something
    var ok = false;
    catch {
        var _ = ({ }.run_with_return scope);
    } [ValueError "e"] {
        assert (e.message == "return wasn't called");
        ok = true;
    };
    assert ok;

    # test early return
    # FIXME: early return isn't supported yet
    #{ return; assert false; }.run_with_return scope;
};

test "implicit returning" {
    assert (({ 123 }.run_with_return {}.definition_scope) == 123);
    assert (({ ";;;" }.run_with_return {}.definition_scope) == ";;;");
    assert (({ {
        throw (new ValueError "this wasn't supposed to run");
    } }.run_with_return {}.definition_scope) `is_instance_of` Block);
    # TODO: make sure that { print "hi"; 123 } throws a syntax error
};
