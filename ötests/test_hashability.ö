var test = (import "utils").test;
var throws = (import "utils").throws;


class "Thingy" { };

var hashables = [
    # pretty much everything, many different kinds of objects
    "lol"
    123
    String
    Thingy
    (new Thingy)
    {print "Hi";}
    {print "Hi";}.definition_scope
    ({print "Hi";}.ast_statements.get 0)
    none
    true
    false
];

# some exceptions
var non_hashables = [
    # arrays constructed in different ways
    []
    {}.ast_statements

    # TODO: (new Option []) shouldn't be hashable

    # mappings constructed in different ways
    (new Mapping)
    {}.definition_scope.local_vars
];

# options of hashables are hashable
# (....slice 0) copies
foreach "hashable" (hashables.slice 0) {
    hashables.push (new Option hashable);
    hashables.push (new Option (new Option hashable));   # options of options... why not
};

# TODO: options of non-hashables should be non-hashable


test "hashables and not-hashables as mapping keys" {
    foreach "hashable" hashables {
        var m = (new Mapping [ [hashable 123] ]);
        assert ((m.get hashable) == 123);
    };

    foreach "non_hashable" non_hashables {
        throws TypeError {
            new Mapping [ [non_hashable 123] ];
        };
    };
};
