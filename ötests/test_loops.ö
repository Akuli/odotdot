var test = (import "utils").test;
var throws = (import "utils").throws;


test "simple stuff" {
    var stuff = [];
    for { var i=0; } { (i != 5) } { i = (i + 1); } {
        stuff.push i;
    };
    assert (stuff == [0 1 2 3 4]);
    throws VariableError {
        # must not leak the i
        debug i;
    };

    var i = 5;
    while { (i != 10) } {
        stuff.push i;
        i = (i + 1);
    };
    assert (stuff == [0 1 2 3 4 5 6 7 8 9]);
    assert (i == 10);   # it wasn't deleted magically, which is good
};

test "for" {
    var what_happened = [];
    for { what_happened.push "init"; var i = 0; }
        { what_happened.push "cond"; return (i != 5); }
        { what_happened.push "incr"; i = (i + 1); }
    {
        what_happened.push "body";
    };

    assert (what_happened == [
        "init"
        "cond" "body" "incr"
        "cond" "body" "incr"
        "cond" "body" "incr"
        "cond" "body" "incr"
        "cond" "body" "incr"
        "cond"
    ]);

    what_happened = [];
    var ok = true;

    throws ArgError {
        for { throw (new ArgError "oops"); } { ok = false; return true; } { ok = false; } { ok = false; };
    };
    assert ok;

    throws ArgError {
        for { } { throw (new ArgError "oops"); } { ok = false; } { ok = false; };
    };
    assert ok;

    var how_many_times = 0;
    throws ArgError {
        for { } { true } { throw (new ArgError "oops"); } { how_many_times = (how_many_times + 1); };
    };
    assert (how_many_times == 1);

    throws ArgError {
        for { } { true } { ok = false; } { throw (new ArgError "oops"); };
    };
    assert ok;
};

test "scopes" {
    var scopes = [];

    # good luck with understanding this code
    var funny_body = ((lambda "" returning:true { {scopes.push {}.definition_scope;} }));
    assert (funny_body.definition_scope.parent_scope.value `same_object` {}.definition_scope);

    for { scopes.push {}.definition_scope; var i = 0; }
        { scopes.push {}.definition_scope; return (i != 2); }
        { scopes.push {}.definition_scope; i = (i + 1); }
        funny_body;

    # a scope should have gotten pushed exactly 8 times, and must be the same scope every time
    assert (scopes.length == 8);
    foreach "scope" scopes {
        assert (scope `same_object` (scopes.get 0));
    };

    # that scope should be a subscope of the body's definition scope
    assert ((scopes.get 0).parent_scope.value `same_object` funny_body.definition_scope);
};

test "return in loop" {
    # the condition of a for or while loop works with a return
    # { x } is same as { return x; }
    # there was a bug that the return variable wasn't removed after running that
    # returning in the loop would call the condition's return instead of the function's return
    # functions like this one threw MarkerError
    func "does_this_break" returning:true {
        for { var i=0; } { (i<10) } { i = (i+1); } {
            if (i > 5) {
                return i;
            };
        };
    };
    assert ((does_this_break) == 6);

    # returning from other places must be allowed too
    func "f" returning:true {
        for { return "x"; } { true } { } { };
    };
    func "g" returning:true {
        for { } { true } { return "y"; } { };
    };
    assert ((f) == "x");
    assert ((g) == "y");
};
