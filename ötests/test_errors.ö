var test = (import "utils").test;
var throws = (import "utils").throws;
var ends_with = (import "utils").ends_with;


test "message attribute" {
    var error = (new TypeError "asd asd");
    assert (error.message == "asd asd");
    var asd_debug_string = (error.to_debug_string);

    error.message = "toot";
    assert (error.message == "toot");
    assert ((error.to_debug_string) != asd_debug_string);

    throws TypeError {
        error.message = 123;
    };
};


class "AsdTootError" inherits:Error { };
class "AsdError" inherits:AsdTootError { };
class "TootError" inherits:AsdTootError { };


test "to_debug_string" {
    assert (((new TypeError "omg").to_debug_string) == "<TypeError: \"omg\">");
    assert (((new Error "").to_debug_string) == "<Error: \"\">");
    assert (((new AsdError "wtf").to_debug_string) == "<AsdError: \"wtf\">");
};

test "throw" {
    throws TypeError { throw "asd"; };
    throws ArgError { throw 1 2 3; };
    throws ArgError { throw (new TypeError "asd") stupid_option:123; };

    # these must be checked with 2 exceptions
    # if throw is failing unexpectedly, it might throw TypeError or ArgError
    # but not both
    throws TypeError { throw (new TypeError "asd"); };
    throws ArgError { throw (new ArgError "asd"); };
};

test "catch" {
    # success case
    var ran = false;
    catch {
    } AsdError {
        ran = true;
    };
    assert (not ran);

    # catching by superclass
    var ran = false;
    catch {
        throw (new AsdError "lol");
    } AsdTootError {
        ran = true;
    };
    assert ran;

    # not catching by subclass
    throws AsdTootError {
        catch {
            throw (new AsdTootError "lol");
        } AsdError { };
    };

    # catch block throws
    throws TootError {
        catch {
            throw (new AsdError "lol");
        } AsdError {
            throw (new TootError "lol");
        };
    };

    # throw and catch must not copy exception objects
    var ran = false;
    var e1 = (new TypeError "asd");
    catch {
        throw e1;
    } [TypeError "e2"] {
        assert (e1 `same_object` e2);
        ran = true;
    };
    assert ran;

    # the "what to catch" argument of catch is handled specially, it's not just a type check
    # so make sure that works
    throws TypeError { catch {} "lol" {}; };
    throws TypeError { catch {} String {}; };   # String is not an error class
    throws TypeError { catch {} [AsdError 123] {}; };   # String is not an error class
    throws TypeError { catch {} [123 "hi"] {}; };   # String is not an error class
    throws ValueError { catch {} [] {}; };
    throws ValueError { catch {} [AsdError] {}; };
    throws ValueError { catch {} [AsdError "lol" 123] {}; };
};

test "the stack attribute" {
    var e = (new AsdError "lol");
    assert e.stack.is_none;

    throws TypeError { e.stack = "lol"; };
    assert e.stack.is_none;

    throws TypeError {
        e.stack = [1 2 3];
    };
    assert e.stack.is_none;

    # right now any arrays are allowed
    # because e.stack is an array, and anything can be added to arrays at any time
    var option = (new Option [1 2 3]);
    e.stack = option;
    assert (e.stack.value == [1 2 3]);
    assert (e.stack.value `same_object` option.value);

    e.stack = none;
    assert e.stack.is_none;
};

test "how throw sets the stack attribute" {
    var e = (new AsdError "lol");
    assert e.stack.is_none;

    var throwscope = "lol";
    throws AsdError {
        throwscope = {}.definition_scope;
        throw e;
    };
    assert (throwscope `is_instance_of` Scope);
    assert (e.stack.value `is_instance_of` Array);

    # lineno isn't cheked exactly because changing the tests changes line numbers
    var last_frame = (e.stack.value.get (e.stack.value.length - 1));
    assert (last_frame.scope `same_object` throwscope);
    assert (last_frame.filename `ends_with` "ötests/test_errors.ö");
    assert (last_frame.lineno `is_instance_of` Integer);
};

test "that the stack doesn't get set on rethrowing" {
    var e = (new AsdError "lol");
    throws AsdError {
        throw e;
    };

    var old_stack = e.stack.value;
    var old_stack_copy = (e.stack.value.slice 0);   # because rethrowing must not change the content of the array
    assert (not (old_stack `same_object` old_stack_copy));
    assert (old_stack == old_stack_copy);

    throws AsdError {
        throw e;
    };
    assert (e.stack.value `same_object` old_stack);
    assert (e.stack.value == old_stack_copy);
};
