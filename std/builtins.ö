var Class = (get_class String);

# errors are defined as early as possible because rest of the code may throw them
var ArgError = (new Class "ArgError" Error);
var AssertError = (new Class "AssertError" Error);
var AttribError = (new Class "AttribError" Error);
var KeyError = (new Class "KeyError" Error);
var MathError = (new Class "MathError" Error);
var TypeError = (new Class "TypeError" Error);
var ValueError = (new Class "ValueError" Error);
var VariableError = (new Class "VariableError" Error);

{}.definition_scope.local_vars.delete "Class";

var add_method = (lambda "klass name lambda_with_this" {
    var getter = (lambda "this" {
        var result = (lambda_with_this.partial this);
        result.name = (name + " method");
        return result;
    });

    getter.name = ("getter of " + name);
    klass.getters.set name getter;
});

add_method Bool "setup" (lambda "this" {
    throw (new TypeError "cannot create new Bool objects, use true and false instead");
});

add_method Bool "to_debug_string" (lambda "this" {
    return ((new Mapping [[true "true"] [false "false"]]).get this);
});

add_method Mapping "get_with_fallback" (lambda "this key fallback" {
    catch {
        return (this.get key);
    } KeyError {
        return fallback;
    };
});

# TODO: how about putting the array inside itself? python does this:
# >>> asd = [1, 2, 3]
# >>> asd.append(asd)
# >>> asd
# [1, 2, 3, [...]]
add_method String "to_debug_string" (lambda "this" {
    var result = "";

    # not very efficient, but that's not a problem because this method is just for debugging anyway
    for { var i=0; } { (i != this.length) } { i = (i + 1); } {
        var character = (this.get i);

        # FIXME: need elif or switch/case or something
        if (character == "\n") {
            result = (result + "\\n");
        } else: {
            if (character == "\t") {
                result = (result + "\\t");
            } else: {
                if (character == "\\") {
                    result = (result + "\\\\");
                } else: {
                    if (character == "\"") {
                        result = (result + "\\\"");
                    } else: {
                        result = (result + character);
                    };
                };
            };
        };
    };

    return (("\"" + result) + "\"");
});

add_method Integer "to_debug_string" (lambda "this" {
    if (this >= 0) {
        # "123"
        return (this.to_string);
    };

    # -1234 is not valid ร code, but (0-1234) is
    return (("(0" + (this.to_string)) + ")");
});

add_method Array "to_debug_string" (lambda "this" {
    if (this.length == 0) {
        return "[]";
    };

    var result = ((this.get 0).to_debug_string);
    for { var i=1; } { (i < this.length) } { i = (i+1); } {
        result = ((result + " ") + ((this.get i).to_debug_string));
    };
    return (("[" + result) + "]");
});


# scope magic: variables created inside this thing are not visible outside it
{
    # get the StackFrame class
    var StackFrame = null;
    catch {
        throw (new MathError "1 + 2 is not 3");
    } [MathError "e"] {
        StackFrame = (get_class (e.stack.get 0));
    };
    if (StackFrame `same_object` null) {
        throw (new AssertError "throw and catch don't seem to work");
    };

    add_method StackFrame "to_string" (lambda "this" {
        return ((("file " + this.filename) + ", line ") + (this.lineno.to_string));
    });

    add_method StackFrame "to_debug_string" (lambda "this" {
        return (("<StackFrame: " + (this.to_string)) + ">");
    });
}.run (new Scope {}.definition_scope);


add_method Error "print_stack" (lambda "this" {
    # TODO: use stderr instead of stdout
    catch {
        print (((get_class this).name + ": ") + this.message);

        if (not (this.stack `same_object` null)) {
            var prefix = "in";
            for { var stack_copy = (this.stack.slice 0); } { (stack_copy.length != 0) } {} {
                var frame = (stack_copy.pop);
                print ((("  " + prefix) + " ") + (frame.to_string));
                # TODO: print the source line

                if (prefix == "in") {
                    prefix = "by";
                };
            };
        };
    } Error {    # avoid recursion!
        print "got another error while printing the stack trace of an error";
    };
});


var func = (lambda "funcname_and_argnames block" {
    var splitted = (funcname_and_argnames.split_by_whitespace);
    if (splitted.length == 0) {
        throw (new ValueError "func's string argument must not be empty");
    };

    # a stupid way to join strings by spaces
    # initial i=1 skips the function name
    var argnames = "";
    for { var i = 1; } { (i < splitted.length) } { i = (i + 1); } {
        argnames = (argnames + (splitted.get i));
        argnames = (argnames + " ");
    };

    var result = (lambda argnames block);
    result.name = (splitted.get 0);
    block.definition_scope.local_vars.set result.name result;
});
func.name = "func";

func "while condition body" {
    for {} condition {} body;
};

func "debug x" {
    print (x.to_debug_string);
};

func "not x" {
    # 'if x' throws an error if x is not a boolean
    if x {
        return false;
    };
    return true;
};

func "and x y" {
    if x {
        if y {
            return true;
        };
    } else: {
        # no matter what x is, an error must be thrown if y is not a Bool
        # this checks it :D
        not y;
    };

    return false;
};

func "or x y" {
    return (not ((not x) `and` (not y)));
};

func "assert condition" {
    if (not condition) {
        throw (new AssertError "assertion failed");
    };
};

# example:
#
#    foreach "x" [1 2 3] {
#        print (x.to_string);
#    };
func "foreach varname array block" {
    var scope = (new Scope block.definition_scope);
    for { var i = 0; } { (i != array.length) } { i = (i + 1); }
    {
        scope.local_vars.set varname (array.get i);
        block.run scope;
    };
};


{
    # gets and deletes a variable from built-in scope
    var get_and_hide_builtin = {}.definition_scope.parent_scope.local_vars.get_and_delete;

    # objects have an attrdata mapping that attributes can store their values in
    # it's not useful anywhere else because one can just create attributes using this code
    # so we hide it from the rest of the world here
    var get_attrdata = (get_and_hide_builtin "get_attrdata");
    var add_method = (get_and_hide_builtin "add_method");

    func "class class_name initializer_block inherits:" {
        if (inherits `same_object` null) {
            inherits = Object;
        };

        var klass = (new (get_class String) class_name inherits);

        func "attrib name" {
            # just to make debugging easier
            if (not (name `is_instance_of` String)) {
                throw (new TypeError ("expected a string, got " + (name.to_debug_string)));
            };

            var getter = (lambda "this" {
                # TODO: throw an error with a descriptive message if the name is missing
                var attrdata = (get_attrdata this);
                return (attrdata.get name);
            });
            var setter = (lambda "this value" {
                var attrdata = (get_attrdata this);
                attrdata.set name value;
            });

            getter.name = ("getter of " + name);
            setter.name = ("setter of " + name);
            klass.getters.set name getter;
            klass.setters.set name setter;
        };

        func "method methname_and_argnames block" {
            # see func for details, some of this is copied from there
            var splitted = (methname_and_argnames.split_by_whitespace);
            if (splitted.length == 0) {
                throw (new ValueError "method's string argument must not be \"\"");
            };

            # i=1 skips the method name
            var this_and_args = "this";
            for { var i = 1; } { (i != splitted.length) } { i = (i + 1); } {
                this_and_args = (this_and_args + " ");
                this_and_args = (this_and_args + (splitted.get i));
            };

            add_method klass (splitted.get 0) (lambda this_and_args block);
        };

        var init_scope = (new Scope initializer_block.definition_scope);
        init_scope.local_vars.set "attrib" attrib;
        init_scope.local_vars.set "method" method;
        initializer_block.run init_scope;

        initializer_block.definition_scope.local_vars.set class_name klass;
    };

    # export class to the built-in scope, but keep get_attrdata here
    {}.definition_scope.parent_scope.local_vars.set "class" class;
}.run (new Scope {}.definition_scope);


{
    # allow importers to be deleted from the built-in scope
    var importers = importers;
    var Library = null;

    func "import string" {
        # last frame is this function, get the one before that
        var stack = (get_stack);
        var frame = (stack.get (stack.length - 2));

        foreach "importer" importers {
            var lib = (importer string frame);

            if (not (lib `same_object` null)) {
                if (Library `same_object` null) {
                    # this is a funny way to get the Library class
                    # imports.รถ gets it by importing a library
                    # but that's what we're doing here all the time, so...
                    Library = (get_class lib);
                } else: {
                    if (not (lib `is_instance_of` Library)) {
                        # need string formatting :(
                        throw (new TypeError ((("importers should return a Library object or null, but " +
                            (importer.to_debug_string)) + " returned ") + (lib.to_debug_string)));
                    };
                };
                return lib;
            };
        };

        # FIXME: ValueError feels wrong
        throw (new ValueError ("cannot import " + (string.to_debug_string)));
    };

    {}.definition_scope.parent_scope.local_vars.set "import" import;
}.run (new Scope {}.definition_scope);

# these files do magic, they delete stuff from the built-in scope
# see comments in these files for details
import "<std>/imports";
import "<std>/operators";
