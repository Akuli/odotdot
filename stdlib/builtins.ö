# FIXME: these suck
var true = "true";
var false = "false";
var null = "null";

# the C code doesn't set this to null because null doesn't exist yet
{}.definition_scope.parent_scope = null;

var not = (new Mapping [[true false] [false true]])::get;

# store if_mapping and raw_if permanently, but hide them in a subscope
var if = null;   # set soon
{
    var if_mapping = (new Mapping [
        [ true { body::run (new Scope body.definition_scope); } ]
        [ false { } ]
    ]);

    var raw_if = (array_func {
        var cond = (arguments::get 0);
        var body = (arguments::get 1);

        (if_mapping::get cond)::run {}.definition_scope;
    });

    if = (array_func {
        raw_if (not ((arguments::get_length) `equals` 2)) {
            error "if takes exactly 2 arguments";
        };
        # TODO: type checks
        raw_if (arguments::get 0) (arguments::get 1);
    });
}::run (new Scope {}.definition_scope);

# usage: assert COND;
var assert = (array_func {
    var cond = (arguments::get 0);
    if (not cond) {
        # there's no throw function yet, but this fails anyway
        # the error message is "no variable named 'throw'", but... lol, it works
        throw "assertion failed";
    };
});


# VERY STUPID
# usage example:
#
#   # strip spaces from a string
#   var s = "      lol";
#   while { keep_going = ((s::get 0) `equals` " "); } {
#       s = (s::slice 1);
#   };
#   print s;    # prints "lol"
#
# infinite loops segfault
var while = (array_func {
    var cond = (arguments::get 0);
    var body = (arguments::get 1);

    var scope = (new Scope body.definition_scope);
    scope.local_vars::set "keep_going" null;

    var run_stuff = {
        cond::run scope;
        if (scope.local_vars::get "keep_going") {
            body::run scope;
            run_stuff::run run_stuff.definition_scope;
        };
    };
    run_stuff::run run_stuff.definition_scope;
});

# usage example:
#
#    foreach "x" [1 2 3] {
#        print (x::to_string);
#    };
#
var foreach = (array_func {
    var varname = (arguments::get 0);
    var array = (arguments::get 1);
    var block = (arguments::get 2);

    var scope = (new Scope block.definition_scope);
    var looped = [];
    while { keep_going = (not (looped `equals` array)); } {
        var elem = (array::get (looped::get_length));
        scope.local_vars::set varname elem;
        block::run scope;
        looped::push elem;
    };
});
