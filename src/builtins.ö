# this file runs in the built-in scope on interpreter startup
# when the interpreter is compiled, this file is included in the executable in a funny way
# see the Makefile and run.c for more about how running this code works

var Class = (get_class String);

# errors are defined as early as possible because rest of the code may throw them
var ArgError = (new Class "ArgError" Error);
var AssertError = (new Class "AssertError" Error);
var AttribError = (new Class "AttribError" Error);
var IoError = (new Class "IoError" Error);
var KeyError = (new Class "KeyError" Error);
var MathError = (new Class "MathError" Error);
var TypeError = (new Class "TypeError" Error);
var ValueError = (new Class "ValueError" Error);
var VariableError = (new Class "VariableError" Error);

# i need to use get_and_delete because delete is defined later
# but returned values are not ignored... so, have fun figuring this out
(lambda "f x" { var _ = (f x); }) {}.definition_scope.local_vars.get_and_delete "Class";

var add_method = (lambda "klass name lambda_with_this" {
    var getter = (lambda "this" returning:true {
        var result = (lambda_with_this.partial this);
        result.name = (name + " method");
        return result;
    });

    getter.name = ("getter of " + name);
    klass.getters.set name getter;
});

add_method (get_class add_method) "to_debug_string" (lambda "this" returning:true {
    return (("<Function " + (this.name.to_debug_string)) + ">");
});

add_method (get_class String) "to_debug_string" (lambda "this" returning:true {
    return (("<Class " + (this.name.to_debug_string)) + ">");
});

add_method Error "to_debug_string" (lambda "this" returning:true {
    return (((("<" + (get_class this).name) + ": ") + (this.message.to_debug_string)) + ">");
});

add_method Bool "setup" (lambda "this" {
    throw (new TypeError "cannot create new Bool objects, use true and false instead");
});

add_method Bool "to_debug_string" (lambda "this" returning:true {
    return ((new Mapping [[true "true"] [false "false"]]).get this);
});

add_method Option "to_debug_string" (lambda "this" returning:true {
    if this.is_none {
        return "none";
    };
    return (("<Option: " + ((this.get_value).to_debug_string)) + ">");
});

add_method Option "get_with_fallback" (lambda "this default" returning:true {
    if this.is_none {
        return default;
    };
    return (this.get_value);
});

add_method Mapping "delete" (lambda "this key" {
    var _ = (this.get_and_delete key);
});

# TODO: use an option instead
add_method Mapping "get_with_fallback" (lambda "this key fallback" {
    catch {
        return (this.get key);
    } KeyError {
        return fallback;
    };
});

add_method Integer "to_debug_string" (lambda "this" returning:true {
    if (this >= 0) {
        # "123"
        return (this.to_string);
    };

    # -1234 is not valid Ã– code, but (0-1234) is
    return (("(0" + (this.to_string)) + ")");
});

add_method Array "to_debug_string" (lambda "this" returning:true {
    if (this.length == 0) {
        return "[]";
    };

    var result = ((this.get 0).to_debug_string);
    for { var i=1; } { (i < this.length) } { i = (i+1); } {
        result = ((result + " ") + ((this.get i).to_debug_string));
    };
    return (("[" + result) + "]");
});


# scope magic: variables created inside this thing are not visible outside it
{
    # get the StackFrame class
    var stackframe_option = none;
    catch {
        throw (new MathError "1 + 2 is not 3");
    } [MathError "e"] {
        stackframe_option = (new Option (get_class ((e.stack.get_value).get 0)));
    };

    add_method (stackframe_option.get_value) "to_string" (lambda "this" returning:true {
        return ((("file " + this.filename) + ", line ") + (this.lineno.to_string));
    });

    add_method (stackframe_option.get_value) "to_debug_string" (lambda "this" returning:true {
        return (("<StackFrame: " + (this.to_string)) + ">");
    });
}.run (new Scope {}.definition_scope);


add_method Error "print_stack" (lambda "this" {
    # TODO: use stderr instead of stdout
    catch {
        print (((get_class this).name + ": ") + this.message);

        var prefix = "in";
        for { var stack_copy = ((this.stack.get_with_fallback []).slice 0); } { (stack_copy.length != 0) } {} {
            var frame = (stack_copy.pop);
            print ((("  " + prefix) + " ") + (frame.to_string));
            # TODO: print the source line

            if (prefix == "in") {
                prefix = "by";
            };
        };
    } [Error "e"] {    # avoid recursion!
        catch {
            print "got another error while printing the stack trace of an error";
            print (get_class e).name;
            print e.message;
        } Error {
            # printing doesn't seem to work, which is not impossible
            # stdout could be closed or whatever
            # or everything's just so fucked up that the error doesn't have a name or whatever
            # not much can be done here
        };
    };
});


var func = (lambda "funcname_and_argnames block returning?" {
    var splitted = (funcname_and_argnames.split_by_whitespace);
    if (splitted.length == 0) {
        throw (new ValueError "func's string argument must not be empty");
    };

    # a stupid way to join strings by spaces
    # initial i=1 skips the function name
    var argnames = "";
    for { var i = 1; } { (i < splitted.length) } { i = (i + 1); } {
        argnames = (argnames + (splitted.get i));
        argnames = (argnames + " ");
    };

    var result = (lambda argnames block returning:(returning.get_with_fallback false));
    result.name = (splitted.get 0);
    block.definition_scope.local_vars.set result.name result;
});
func.name = "func";

func "while condition body" {
    for {} condition {} body;
};


func "switch value switchblock" returning:true {
    var switchscope = (new Scope switchblock.definition_scope);

    func "case match block" {
        if (value == match) {
            block.run (new Scope switchscope);
        };
    };

    func "default block" {
        # always run, because this is after cases that return
        block.run (new Scope switchscope);
    };

    switchscope.local_vars.set "case" case;
    switchscope.local_vars.set "default" default;
    switchscope.local_vars.set "return" return;
    switchblock.run switchscope;
};

# TODO: how about putting the array inside itself? python does this:
# >>> asd = [1, 2, 3]
# >>> asd.append(asd)
# >>> asd
# [1, 2, 3, [...]]
add_method String "to_debug_string" (lambda "this" returning:true {
    var result = "";

    for { var i=0; } { (i != this.length) } { i = (i + 1); } {
        var character = (this.get i);

        # not very efficient, but that's not a problem because this method is just for debugging anyway
        result = (result + (switch character {
            case "\n" { "\\n" };
            case "\t" { "\\t" };
            case "\\" { "\\\\" };
            case "\"" { "\\\"" };
            default { character };
        }));
    };

    return (("\"" + result) + "\"");
});


func "debug x" {
    print (x.to_debug_string);
};

func "not x" returning:true {
    # 'if x' throws an error if x is not a boolean
    if x {
        return false;
    };
    return true;
};

func "and x y" returning:true {
    if x {
        if y {
            return true;
        };
    } else: {
        # no matter what x is, an error must be thrown if y is not a Bool
        # this checks it :D
        var _ = (not y);
    };

    return false;
};

func "or x y" returning:true {
    # have fun figuring this out
    return (not ((not x) `and` (not y)));
};

func "assert condition" {
    if (not condition) {
        throw (new AssertError "assertion failed");
    };
};

# TODO: throw a good error if klass is not actually a class
func "is_instance_of instance klass" returning:true {
    var subclass = (get_class instance);

    # check if 'subclass' is a subclass of 'klass'
    var running = true;
    while { running } {
        if (subclass `same_object` klass) {
            return true;
        };
        if subclass.baseclass.is_none {
            running = false;   # there's no break yet :(
        } else: {
            subclass = (subclass.baseclass.get_value);
        };
    };
    return false;
};

# example:
#
#    foreach "x" [1 2 3] {
#        print (x.to_string);
#    };
func "foreach varname array block" {
    var scope = (new Scope block.definition_scope);
    for { var i = 0; } { (i != array.length) } { i = (i + 1); }
    {
        scope.local_vars.set varname (array.get i);
        block.run scope;
    };
};


{
    # gets and deletes a variable from built-in scope
    var get_and_hide_builtin = ({}.definition_scope.parent_scope.get_value).local_vars.get_and_delete;

    # objects have an attrdata mapping that attributes can store their values in
    # it's not useful anywhere else because one can just create attributes using this code
    # so we hide it from the rest of the world here
    var get_attrdata = (get_and_hide_builtin "get_attrdata");
    var add_method = (get_and_hide_builtin "add_method");

    func "class class_name initializer_block inherits?" {
        var klass = (new (get_class String) class_name (inherits.get_with_fallback Object));

        func "getter name block" {
            var function = (lambda "this" block returning:true);
            function.name = ("getter of " + name);
            klass.getters.set name function;
        };

        func "setter name varname block" {
            var function = (lambda ("this " + varname) block);
            function.name = ("setter of " + name);
            klass.setters.set name function;
        };

        func "abstract name" {
            getter name {
                throw (new AttribError ("cannot get abstract attribute " + (name.to_debug_string)));
            };
        };

        func "attrib name" {
            getter name {
                catch {
                    return ((get_attrdata this).get name);
                } KeyError {
                    throw (new AttribError (("the value of " + (name.to_debug_string)) + " hasn't been set"));
                };
            };
            setter name "new_value" {
                (get_attrdata this).set name new_value;
            };
        };

        func "method methname_and_argnames block returning?" {
            # see func for details, some of this is copied from there
            var splitted = (methname_and_argnames.split_by_whitespace);
            if (splitted.length == 0) {
                throw (new ValueError "method's string argument must not be \"\"");
            };

            # i=1 skips the method name
            var this_and_args = "this";
            for { var i = 1; } { (i != splitted.length) } { i = (i + 1); } {
                this_and_args = (this_and_args + " ");
                this_and_args = (this_and_args + (splitted.get i));
            };

            add_method klass (splitted.get 0) (lambda this_and_args block returning:(returning.get_with_fallback false));
        };

        var init_scope = (new Scope initializer_block.definition_scope);
        init_scope.local_vars.set "getter" getter;
        init_scope.local_vars.set "setter" setter;
        init_scope.local_vars.set "abstract" abstract;
        init_scope.local_vars.set "attrib" attrib;
        init_scope.local_vars.set "method" method;
        initializer_block.run init_scope;

        initializer_block.definition_scope.local_vars.set class_name klass;
    };

    # export class to the built-in scope, but keep get_attrdata and stuff here
    ({}.definition_scope.parent_scope.get_value).local_vars.set "class" class;
}.run (new Scope {}.definition_scope);


{
    # need string formatting :(
    func "error importer returned" {
        throw (new TypeError ((("importers should return an Option of a Library object, but " +
            (importer.to_debug_string)) + " returned ") + (returned.to_debug_string)));
    };

    # allow importers to be deleted from the built-in scope
    var importers = importers;

    # this is a funny way to get the Library class
    # the only way to access it is to import something
    # so for the first import, anything must be allowed
    var Library = Object;

    func "import string" returning:true {
        # get a stack with an error
        var stack = [];
        catch {
            throw (new Error "oh no");
        } [Error "e"] {
            stack = (e.stack.get_value);
        };
        assert (stack.length > 0);

        # last frame is the throw
        # the frame before that is this function
        # we want the one before that
        var frame = (stack.get (stack.length - 3));

        foreach "importer" importers {
            var lib = (importer string frame);
            if (not (lib `is_instance_of` Option)) {
                error importer lib;
            };

            if (not lib.is_none) {
                if (not ((lib.get_value) `is_instance_of` Library)) {
                    error importer lib;
                };

                if (Library `same_object` Object) {
                    Library = (get_class (lib.get_value));
                };
                return (lib.get_value);
            };
        };

        # FIXME: ValueError feels wrong
        throw (new ValueError ("cannot import " + (string.to_debug_string)));
    };

    ({}.definition_scope.parent_scope.get_value).local_vars.set "import" import;
}.run (new Scope {}.definition_scope);

# these files do magic, they delete stuff from the built-in scope
# see comments in these files for details
var _ = (import "<std>/imports");
var _ = (import "<std>/operators");
{}.definition_scope.local_vars.delete "_";
